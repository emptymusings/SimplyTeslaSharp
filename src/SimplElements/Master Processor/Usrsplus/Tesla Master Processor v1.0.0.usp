/*
Programmer: Tim Johnson
Comments: Crestron user module for authorization and high level vehicle communications with Tesla servers
*/

/*******************************************************************************************
  Compiler Directives
  (Uncomment and declare compiler directives as needed)
*******************************************************************************************/
#SYMBOL_NAME "Tesla Master Processor v1.0.0"
#HINT "Master Interactions with Tesla Account"
#DEFAULT_VOLATILE
#ENABLE_STACK_CHECKING
#ENABLE_TRACE

#HELP_BEGIN

This module is designed to interact with Tesla Vehicles.  In order to 
interact with Tesla's servers, you will need to include this Master 
Processor to handle Authentication/Vehicle lists, and the Tesla Car 
Processor module to handle information and commands related to specific 
vehicles.

#HELP_END


/*******************************************************************************************
  Include Libraries
*******************************************************************************************/
// Include the Simpl# module#USER_SIMPLSHARP_

#USER_SIMPLSHARP_LIBRARY  "SimplTeslaMaster"
      

/*******************************************************************************************
  DIGITAL, ANALOG and SERIAL INPUTS and OUTPUTS
*******************************************************************************************/
DIGITAL_INPUT 
			RequestToken, 
			RefreshCarList;
STRING_INPUT 
			email[65534], 
			password[65534];	
ANALOG_OUTPUT
			CarCount;
STRING_OUTPUT 
			TokenFb, 
			TokenCreatedFb, 
			TokenExpiresFb,
			RefreshTokenFb,
			CarId$[99];
DIGITAL_OUTPUT
			IsLoggedIn;

/*******************************************************************************************
  Global Variables
*******************************************************************************************/
Main tesla;

/*******************************************************************************************
  Functions
*******************************************************************************************/
// Initializes the necessary login information in the Simpl# module in order to authenticate
// for the authorization server
Function InitializeTesla()
{
	// Send the login information to the Simpl# module's cache
	tesla.Auth.Initialize(email, password);
}

// Requests an authorization token from Tesla's servers    
Function GetTokenFromApi()
{	
	// Set the login information for authentication
	InitializeTesla();
                      
	// Request a Token from the Tesla server
	tesla.Auth.SendTokenRequest();
        
	// Set the outputs for authentication information
	TokenFb = tesla.Auth.GetCurrentToken();
	TokenCreatedFb = tesla.Auth.TokenCreated;
	TokenExpiresFb = tesla.Auth.TokenExpires;
	RefreshTokenFb = tesla.Auth.RefreshToken;
	
	if (LEN(tesla.Auth.GetCurrentToken()) > 0)
	{
     	IsLoggedIn = 1;
	}
	else
	{
     	IsLoggedIn = 0;
	}
}
      
// Retrieves a list of all vehicles associated with the user's account
Function GetCarList()
{
	INTEGER i;
	STRING carVal[255];
    VehicleListModel carList;
	
	// Send the command to request the list of vehicles from Tesla's servers                  
 	tesla.RefreshCars();
    
	// Update the local list of cars to the updated list from the request
	carList = tesla.Cars;     
                              
	// Get the total number of cars associated with the user's accoun
	CarCount = tesla.CarCount;
    
	// For each vehicle that has been returned, assign its ID to the appropriate output signal                          
	for (i = 1 to CarCount)
	{		
		if (IsSignalDefined(CarId$[i]))
		{
			PRINT("Set ID for Car %u",i);
			CarId$[i] = carList.Vehicles[i-1].Id;
		}
	}	
}


/*******************************************************************************************
  Event Handlers
*******************************************************************************************/

ThreadSafe CHANGE email
{
	tesla.Auth.UserName = email;}

ThreadSafe CHANGE password
{
	tesla.Auth.Password = password;
}


ThreadSafe PUSH RequestToken
{
	GetTokenFromApi();
}

ThreadSafe PUSH RefreshCarList
{
	GetCarList();
}
